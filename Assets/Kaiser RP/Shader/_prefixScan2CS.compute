#pragma kernel CSMain

#include "UnityShaderVariables.cginc"
#include "../Include/frame_constants.hlsl"

#define THREAD_GROUP_SIZE 512
#define SEGMENT_SIZE (THREAD_GROUP_SIZE * 2)

ByteAddressBuffer input_buf : register(t0);

RWByteAddressBuffer output_buf : register(u0);

groupshared uint shared_data[SEGMENT_SIZE];

uint load_input(uint idx)
{
    const uint segment_sum_idx = idx * SEGMENT_SIZE + SEGMENT_SIZE - 1;
    return input_buf.Load(4 * segment_sum_idx);
}

void store_output2(uint idx, uint2 val)
{
    output_buf.Store2(4 * idx, val);
}

[numthreads(THREAD_GROUP_SIZE,1,1)]
void CSMain (uint3 GroupId : SV_GroupID,
    uint3 DispatchThreadId : SV_DispatchThreadID,
    uint3 GroupThreadId : SV_GroupThreadID)
{
    uint idx = GroupThreadId.x;
    uint segment = GroupId.x;
    const uint STEP_COUNT = uint(log2(THREAD_GROUP_SIZE)) + 1;

    shared_data[idx * 2] = load_input(idx * 2);
    shared_data[idx * 2 + 1] = load_input(idx * 2 + 1);

    GroupMemoryBarrierWithGroupSync();

    [unroll]
    for (uint step = 0; step < STEP_COUNT; step++)
    {
        uint mask = (1u << step) - 1;
        uint rd_idx = ((idx >> step) << (step + 1)) + mask;
        uint wr_idx = rd_idx + 1 + (idx & mask);

        shared_data[wr_idx] += shared_data[rd_idx];

        GroupMemoryBarrierWithGroupSync();
    }

    store_output2(idx * 2, uint2(shared_data[idx * 2], shared_data[idx * 2 + 1]));
}
